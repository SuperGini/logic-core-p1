plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.5'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'com.gini'
version = '1.0.0'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('testcontainersVersion', "1.18.1")
}

dependencies {

    implementation group: 'com.github.gavlyukovskiy', name: 'datasource-proxy-spring-boot-starter', version: '1.9.0'
    implementation group: 'io.hypersistence', name: 'hypersistence-utils-hibernate-60', version: '3.2.0'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    compileOnly 'org.projectlombok:lombok'

    runtimeOnly 'com.mysql:mysql-connector-j'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mysql'
//    logging
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-spring-boot
   // implementation group: 'org.apache.logging.log4j', name: 'log4j-spring-boot', version: '2.20.0'


}

//exclude log4j-to-slf4j because i get an error when importing log4j-spring-boot
configurations.all {
    exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}


//START ---- this is need it to configure integrationTest folder as a module for integration tests----------------------
//https://javacodehouse.com/blog/spring-boot-integration-test-gradle-setup/
sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

tasks.register('integrationTest', Test) {
    description = "Run integration tests"
    group = "verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}


configurations {
    integrationTestImplementation.extendsFrom(testImplementation)
    integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}

tasks.named('integrationTest') {
    useJUnitPlatform()
}
//END integrationTest configuration module------------------------------------------------------------------------------



tasks.named('test') {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Start-Class': 'com.gini.LogicCoreApplication'
        archiveFileName = 'my-application.jar'
    }
}





